import java.text.SimpleDateFormat

buildscript {

	// ---------------------------------------------------------------------------
	// Project targets the following versions of libraries, tools etc..	
	ext.targetH2Version 		= '1.3.176'
	ext.targetSpringVersion 	= '4.1.6.RELEASE'
	ext.targetJUnitVersion 		= '4.12'
	ext.targetSLF4JVersion 		= '1.7.12'
	ext.javaBuildVersion 		= '1.8'
	ext.springBootVersion		= '1.2.3.RELEASE'

	repositories {
		mavenLocal()
		jcenter()
	}
		
}


plugins {
	id 'com.jfrog.bintray' version '1.0'
}
	
ext.currentVersion = '1.0.0-SNAPSHOT'
ext.projectGroup = 'org.andriesfc.h2-spring-tools'

subprojects {	
	
	repositories {
		jcenter()
	}
	

	apply plugin: 'java'

	configurations {
		all*.exclude group: 'commons-logging', module: 'commons-logging'
	}

	dependencies {
		compile "org.slf4j:jcl-over-slf4j:$targetSLF4JVersion"
		compile "org.slf4j:jul-to-slf4j:$targetSLF4JVersion"
		testCompile "junit:junit:$targetJUnitVersion"
	}

	group = 'org.andriesfc.h2springtools'
	version = currentVersion
	sourceCompatibility = javaBuildVersion
	targetCompatibility = javaBuildVersion


	// ---------------------------------------------------------------------------------------------------------
	// Publishing
	// ---------------------------------------------------------------------------------------------------------

	// add javadoc/source jar tasks as artifacts

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives sourcesJar, javadocJar
	}

	bintray.user = bintray_user
	bintray.key  = bintray_key
}

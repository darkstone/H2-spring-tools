
buildscript {
	
	// ----------------------------------------------------------------------------
	// Standardize module settings
	ext.moduleNamePrefix = 'h2-spring-tools'
	ext.moduleOrg = 'org.andriesfc.h2-spring-tools'
		
	// ---------------------------------------------------------------------------
	// Project targets the following versions of libraries, tools etc..	
	ext.targetH2Version 		= '1.3.176'
	ext.targetSpringVersion 	= '4.1.6.RELEASE'
	ext.targetJUnitVersion 		= '4.12'
	ext.targetSLF4JVersion 		= '1.7.12'
	ext.buildVersion 			= '1.0-SNAPSHOT'
	ext.javaBuildVersion 		= '1.8'
	ext.springBootVersion		= '1.2.3.RELEASE'


	// --------------------------------------------------------------------------
	// Script Extensions 
	ext.qualifyBaseName = { name -> "$moduleNamePrefix-$name" }

	// ----------------------------------------------------------------------------
	// Plugins

	repositories {
		jcenter()
	}

	dependencies {
		classpath  'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
	}

}


subprojects {

	apply plugin: 'maven-publish'
	apply plugin: 'java'
	apply plugin: 'com.jfrog.bintray'

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}


	repositories {
		jcenter()
	}

	configurations {
		all*.exclude group: 'commons-logging', module: 'commons-logging'
	}

	dependencies {
		compile "org.slf4j:jcl-over-slf4j:$targetSLF4JVersion"
		compile "org.slf4j:jul-to-slf4j:$targetSLF4JVersion"
		testCompile "junit:junit:$targetJUnitVersion"
	}
	
	group = moduleOrg
	version = buildVersion


	jar {
		baseName = "${qualifyBaseName(project.name)}"
	}



	sourceCompatibility = javaBuildVersion
	targetCompatibility = javaBuildVersion


	// ---------------------------------------------------------------------------------------------------------
	// Publishing
	// ---------------------------------------------------------------------------------------------------------

	// add javadoc/source jar tasks as artifacts
	artifacts {
		archives sourcesJar, javadocJar
	}


}

